apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.3'
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.sabkayar.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def localPropertiesFile = rootProject.file("local.properties")
        def localProperties = new Properties()
        localProperties.load(new FileInputStream(localPropertiesFile))

        buildConfigField("String","app_id_ad",localProperties['APP_ID_AD'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'TypographyFractions', 'TypographyQuotes'
        abortOnError false
    }

    flavorDimensions("version")
    productFlavors {
        free {
            dimension "version"
            applicationIdSuffix ".free"
        }

        paid {
            dimension "version"
            applicationIdSuffix ".paid"
        }
    }


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.api-client:google-api-client:1.23.0'
    implementation 'com.google.http-client:google-http-client-android:1.23.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    freeImplementation 'com.google.android.gms:play-services-ads:18.3.0'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })


    testImplementation 'junit:junit:4.12'

    implementation project(":JokeTellingLib")
    implementation project(":showjokeandroidlib")
}


task testAndConnectedTest(dependsOn: ['test', 'connectedAndroidTest']) {
    group = 'custom_tasks'
    description = "Run all tests"
}
task testAll {
    dependsOn ':backend:appengineStart'
    dependsOn testAndConnectedTest
    finalizedBy ':backend:appengineStop'
    group = 'custom_tasks'
    description = "Run all tests"
}

testAndConnectedTest.mustRunAfter ':backend:appengineStart'

